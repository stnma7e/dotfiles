* Basic setup

Ensure that package archives are available and setup package infastructure.

#+BEGIN_SRC emacs-lisp
  ;; use straight.el package management
  (defvar bootstrap-version)
  (let ((bootstrap-file
         (expand-file-name
          "straight/repos/straight.el/bootstrap.el"
          (or (bound-and-true-p straight-base-dir)
              user-emacs-directory)))
        (bootstrap-version 7))
    (unless (file-exists-p bootstrap-file)
      (with-current-buffer
          (url-retrieve-synchronously
           "https://raw.githubusercontent.com/radian-software/straight.el/develop/install.el"
           'silent 'inhibit-cookies)
        (goto-char (point-max))
        (eval-print-last-sexp)))
    (load bootstrap-file nil 'nomessage))
  (setq straight-use-package-by-default t)
  (defun my/auto-freeze-and-stage ()
    (interactive)
    "Auto-freeze lockfile and stage it for commit."
    (straight-freeze-versions)
    (message "âœ“ Package versions frozen"))
  ;; Freeze on Emacs exit (safety net)
  (add-hook 'kill-emacs-hook #'my/auto-freeze-and-stage)

  (use-package straight
    :custom
    ;; add project and flymake to the pseudo-packages variable so straight.el doesn't download a separate version than what eglot downloads.
    (straight-built-in-pseudo-packages '(emacs nadvice python image-mode project flymake xref))
    (straight-use-package-by-default t))

  ;; ensure everything is byte-compiled
  ;(byte-recompile-directory (expand-file-name "~/.emacs.d") 0)

  (defun open-config()
    (interactive)
    (find-file (expand-file-name "settings.org" user-emacs-directory)))
#+END_SRC

* Global configs

#+BEGIN_SRC emacs-lisp
  ;; store desktop files per-project so that I can quit and restart emacs
  ;; this is supposed to be like vscode, so that I don't have to reopen all my buffers
  ;; when I quit emacs
  ;; we'll see how useful it is
  (use-package desktop
    :config
    (setq desktop-path (list (expand-file-name "desktops" user-emacs-directory))
          desktop-dirname (expand-file-name "desktops" user-emacs-directory)
          desktop-restore-eager 10
          desktop-lazy-idle-delay 1
          desktop-auto-save-timeout 30)

    ;; Create desktops directory if it doesn't exist
    (unless (file-directory-p desktop-dirname)
      (make-directory desktop-dirname t))

    (defun my/project-desktop-file ()
      "Get desktop file path for current project."
      (when-let ((project (project-current)))
        (let* ((project-root (project-root project))
               ;; Create safe filename from project path
               (project-name (replace-regexp-in-string "/" "_"
                                                      (string-trim project-root "/")))
               (desktop-file (expand-file-name
                             (concat project-name ".desktop")
                             desktop-dirname)))
          desktop-file)))

    (defun my/project-desktop-save ()
      "Save desktop for current project."
      (interactive)
      (if-let ((desktop-file (my/project-desktop-file)))
          (progn
            (desktop-save desktop-dirname)
            ;; Rename to project-specific name
            (let ((default-desktop (expand-file-name ".emacs.desktop" desktop-dirname)))
              (when (file-exists-p default-desktop)
                (rename-file default-desktop desktop-file t)
                (message "Saved desktop: %s" (file-name-nondirectory desktop-file)))))
        (message "Not in a project")))

    (defun my/project-desktop-read ()
      "Load desktop for current project."
      (interactive)
      (if-let ((desktop-file (my/project-desktop-file)))
          (if (file-exists-p desktop-file)
              (progn
                ;; Copy to default name so desktop can read it
                (let ((default-desktop (expand-file-name ".emacs.desktop" desktop-dirname)))
                  (copy-file desktop-file default-desktop t)
                  (desktop-read desktop-dirname)
                  (message "Loaded desktop: %s" (file-name-nondirectory desktop-file))))
            (message "No saved desktop for this project"))
        (message "Not in a project")))

    (defun my/project-desktop-list ()
      "List all saved project desktops."
      (interactive)
      (let ((desktops (directory-files desktop-dirname nil "\\.desktop$")))
        (if desktops
            (message "Saved desktops: %s" (string-join desktops ", "))
          (message "No saved desktops")))))

  (setq scroll-step           1
        scroll-conservatively 10000)
  (xterm-mouse-mode 1)

  (menu-bar-mode -1) ; turn off file menu
  (tool-bar-mode -1) ; turn off the save/open file/etc buttons at the top of emacs
  (scroll-bar-mode -1)
  (setq-default indent-tabs-mode t)
  (add-hook 'before-save-hook 'delete-trailing-whitespace)
  (electric-pair-mode) ; match parentheses when typing

  ;; store all backup and autosave files in the tmp dir
  (setq backup-directory-alist `(("." . ,temporary-file-directory)))

  ;; Line numbers
  (setq-default display-line-numbers-type 'relative)
  (global-display-line-numbers-mode)
  (column-number-mode)
#+END_SRC

* Programming languages

#+BEGIN_SRC emacs-lisp
  (use-package magit)
  (use-package transient)
  (use-package format-all)

  (defun indent-org-block-automatically ()
    (when (org-in-src-block-p)
      (org-edit-special)
      (indent-region (point-min) (point-max))
      (org-edit-src-exit)))

  (use-package blamer
    :defer 20
    :custom
    (blamer-idle-time 0.3)
    (blamer-min-offset 70)
    :custom-face
    (blamer-face ((t :foreground "#7a88cf"
                     :background nil
                     :height 70
                     :italic t))))

  (use-package lispy)

  (use-package go-mode)

  (use-package rust-mode
    :config
    (add-hook 'rust-mode-hook 'eglot-ensure)
    (setq rust-format-on-save t))

  ;; python
  (use-package python
    :config ;; from Claude AI
    (defun my/setup-uv-venv ()
      "Detect and activate uv virtualenv in project."
      (when-let* ((project-root (locate-dominating-file default-directory ".venv"))
                  (venv-path (expand-file-name ".venv" project-root))
                  (python-path (expand-file-name "bin/python" venv-path)))
        (when (file-exists-p python-path)
          (setq-local python-shell-interpreter python-path
                      python-shell-interpreter-args "-i")
          (message "Activated uv venv: %s" python-path))))
    (add-hook 'python-mode-hook #'my/setup-uv-venv)
    ;; Better defaults for Python shell
    (setq python-shell-completion-native-enable nil  ; Avoid warnings
          python-indent-guess-indent-offset-verbose nil))
  (defun pyformat ()
    (interactive)
    (shell-command-on-buffer-impl "pyformat"))
  (use-package code-cells
    :after python
    :hook (python-mode . code-cells-mode)
    :config
    (add-hook 'python-mode-hook 'code-cells-mode-maybe)
    (let ((map code-cells-mode-map))
        (define-key map (kbd "M-p") 'code-cells-backward-cell)
        (define-key map (kbd "M-n") 'code-cells-forward-cell)
        (define-key map (kbd "C-c C-c") 'code-cells-eval)
        ;; Overriding other minor mode bindings requires some insistence...
        (define-key map [remap jupyter-eval-line-or-region] 'code-cells-eval)))
#+END_SRC

* Emacs display configs

** General

Set C-like language configuration for spacing. This includes C/C++, Java, etc.
#+BEGIN_SRC emacs-lisp
  (show-paren-mode 1)
  (save-place-mode 1)
  (global-whitespace-mode 1)
  (setq whitespace-style
  	'(face tabs trailing space-before-tab newline indentation empty space-after-tab tab-mark missing-newline-at-eof))
  (custom-set-faces
   '(whitespace-tab ((t (:foreground "#636363" :background nil)))))

  ;; Disable variable name inlays by default
  (add-hook 'eglot-managed-mode-hook (lambda () (eglot-inlay-hints-mode -1)))

  ;; Set default font size
  (set-face-attribute 'default nil :height 110)

  ;; make popup backgrounds a little darker
  (use-package solaire-mode
    :config
    (solaire-global-mode +1))

  (use-package diff-hl
    :config
    (global-diff-hl-mode))

  (use-package mood-line
    :config
    (mood-line-mode))

  (use-package rainbow-delimiters
    :config
    (add-hook 'prog-mode-hook #'rainbow-delimiters-mode))

  (use-package markdown-mode)
#+END_SRC

** Evil mode

#+BEGIN_SRC emacs-lisp
  (setq evil-want-keybinding nil) ;; required by evil-collection, must be before loading evil
  (use-package undo-fu)
  (use-package vundo)
  (use-package evil-visualstar
    :config
    (global-evil-visualstar-mode))
  (use-package evil
    :custom
    (evil-want-integration t) ;; This is optional since it's already set to t by default.
    (evil-want-C-u-scroll t)
    :config
    (evil-set-undo-system 'undo-fu)
    (evil-select-search-module 'evil-search-module 'evil-search)
    (evil-mode 1))
  (use-package evil-collection
    :after evil
    :config
    (evil-collection-init))
#+END_SRC

** Completion

This section includes a couple different types of completion for Emacs.

One is completion of UI elements, like minibuffers, etc. This is done via
the trilogy of vertico (completion menus), marginalia (completion menu annotations),
and consult (provides "enhanced versions of common Emacs commands with live previews
and better search capabilities").

The other is in-buffer completion. This is more akin to the completion we usually
think of in VS Code, for example. This is provided (at the time of writing) by corfu;
previously it was provided by company.


#+BEGIN_SRC emacs-lisp
  ;; better fuzzy search than flex (whole word matching)
  (use-package hotfuzz
    :config
    (setq completion-styles '(hotfuzz))
    (setq completion-ignore-case t)
    (setq read-file-name-completion-ignore-case t)
    (setq read-buffer-completion-ignore-case t))
  ;; even better fuzzy search that matches on multiple partial word queries at once
  (use-package orderless
    :after hotfuzz
    :config
    (setq completion-styles '(hotfuzz orderless)))
  (use-package vertico
    :after orderless
    :init
    (vertico-mode))

  ;; Enable rich vertico annotations using the Marginalia package
  (use-package marginalia
    ;; Bind `marginalia-cycle' locally in the minibuffer.  To make the binding
    ;; available in the *Completions* buffer, add it to the
    ;; `completion-list-mode-map'.
    :bind (:map minibuffer-local-map
           ("M-A" . marginalia-cycle))
    :init
    (marginalia-mode))

  (use-package consult
    :bind (
           ("C-x b" . consult-buffer)            ; orig. switch-to-buffer
           ("C-s" . consult-line)
           ;; Minibuffer history
           :map minibuffer-local-map
           ("M-s" . consult-history)             ;; orig. next-matching-history-element
           ("M-r" . consult-history))            ;; orig. previous-matching-history-element

    ;; Enable automatic preview at point in the *Completions* buffer. This is
    ;; relevant when you use the default completion UI.
    :hook (completion-list-mode . consult-preview-at-point-mode)

    ;; The :init configuration is always executed (Not lazy)
    :init
    ;; Use Consult to select xref locations with preview
    (setq xref-show-xrefs-function #'consult-xref
          xref-show-definitions-function #'consult-xref)
  )

  ;; in-buffer completion (think vscode code completion)
  (use-package corfu
    :init
    (global-corfu-mode))

  ;; Persist history over Emacs restarts. Vertico sorts by history position.
  (use-package savehist
    :init
    (savehist-mode))

  ;; Persist recently opened files over Emacs restarts. Used by consult-recent-file.
  (use-package recentf
    :config
    (recentf-mode 1)
    (setq recentf-max-saved-items 200
          recentf-max-menu-items 20)
    ;; Exclude some paths from recent files
    (add-to-list 'recentf-exclude "/tmp/")
    (add-to-list 'recentf-exclude "/ssh:")
    (add-to-list 'recentf-exclude "~/.emacs.d/straight/")
    ;; Save the recent files list periodically
    (run-at-time nil (* 5 60) 'recentf-save-list)  ; Save every 5 minutes
  )

  ;; A few more useful configurations...
  (use-package emacs
    :init
    ;; Add prompt indicator to `completing-read-multiple'.
    ;; We display [CRM<separator>], e.g., [CRM,] if the separator is a comma.
    (defun crm-indicator (args)
      (cons (format "[CRM%s] %s"
                    (replace-regexp-in-string
                     "\\`\\[.*?]\\*\\|\\[.*?]\\*\\'" ""
                     crm-separator)
                    (car args))
            (cdr args)))
    (advice-add #'completing-read-multiple :filter-args #'crm-indicator)

    ;; Do not allow the cursor in the minibuffer prompt
    (setq minibuffer-prompt-properties
          '(read-only t cursor-intangible t face minibuffer-prompt))
    (add-hook 'minibuffer-setup-hook #'cursor-intangible-mode)

    ;; Emacs 28: Hide commands in M-x which do not work in the current mode.
    ;; Vertico commands are hidden in normal buffers.
    (setq read-extended-command-predicate
          #'command-completion-default-include-p)

    ;; Enable recursive minibuffers
    (setq enable-recursive-minibuffers t))

  (use-package eglot
    :config
    (add-hook 'go-mode-hook 'eglot-ensure))

  (use-package which-key
    :init
    (setq-default which-key-idle-delay 0.25)
    :config
    ;; Lower which-key latency
    (which-key-mode 1))

  ;; Ensure that filenames are unique and retain information about their directory.
  (require 'uniquify)
  (setq uniquify-buffer-name-style 'forward
        uniquify-after-kill-buffer-p t ; rename after killing
  )
#+END_SRC

* Utilities
#+BEGIN_SRC emacs-lisp
  (defun kill-mode-buffers (mode)
    (mapc (lambda (buffer)
            (when (eq mode (buffer-local-value 'major-mode buffer))
              (kill-buffer buffer)))
          (buffer-list)))
  (defun kill-elisp-buffers ()
    (interactive)
    (kill-mode-buffers 'emacs-lisp-mode))
  (defun kill-go-buffers ()
    (interactive)
    (kill-mode-buffers 'go-mode))
  (defun kill-starlark-buffers ()
    (interactive)
    (kill-mode-buffers 'bazel-starlark-mode)
    (kill-mode-buffers 'skylark-mode))
  (defun kill-build-buffers ()
    (interactive)
    (kill-mode-buffers 'bazel-build-mode))

   (defun run-in-vterm-kill (process event)
     "A process sentinel. Kills PROCESS's buffer if it is live."
     (let ((b (process-buffer process)))
       (and (buffer-live-p b)
            (kill-buffer b))))

   (defun run-in-vterm (command)
     "Execute string COMMAND in a new vterm.

   Interactively, prompt for COMMAND with the current buffer's file
   name supplied. When called from Dired, supply the name of the
   file at point.

   Like `async-shell-command`, but run in a vterm for full terminal features.

   The new vterm buffer is named in the form `*foo bar.baz*`, the
   command and its arguments in earmuffs.

   When the command terminates, the shell remains open, but when the
   shell exits, the buffer is killed."
     (interactive
      (list
       (let* ((f (cond (buffer-file-name)
                       ((eq major-mode 'dired-mode)
                        (dired-get-filename nil t))))
              (filename (concat " " (shell-quote-argument (and f (file-relative-name f))))))
         (read-shell-command "Terminal command: "
                             (cons filename 0)
                             (cons 'shell-command-history 1)
                             (list filename)))))
     (with-current-buffer (vterm (concat "*" command "*"))
       (set-process-sentinel vterm--process #'run-in-vterm-kill)
       (vterm-send-string command)
       (vterm-send-return)))

   (defun shell-command-on-buffer-impl (command)
     (let ((line (line-number-at-pos)))
       ;; replace buffer with output of shell command
       (shell-command-on-region (point-min) (point-max) command nil t)
       ;; restore cursor position
       (goto-line line)
       (recenter-top-bottom)))

   (defun shell-command-on-buffer ()
     (interactive)
     (shell-command-on-buffer-impl (read-shell-command "Shell command on buffer: ")))
 #+END_SRC

 * Themes

#+BEGIN_SRC emacs-lisp
  (use-package color-theme-sanityinc-tomorrow)

  (use-package doom-themes
    :config
    ;; Global settings (defaults)
    (setq doom-themes-enable-bold t    ; if nil, bold is universally disabled
          doom-themes-enable-italic t) ; if nil, italics is universally disabled
    ;(load-theme 'doom-spacegrey t)

    ;; Enable flashing mode-line on errors
    (doom-themes-visual-bell-config)
    ;; Enable custom neotree theme (all-the-icons must be installed!)
    (doom-themes-neotree-config)
    ;; or for treemacs users
    (setq doom-themes-treemacs-theme "doom-atom") ; use "doom-colors" for less minimal icon theme
    (doom-themes-treemacs-config)
    ;; Corrects (and improves) org-mode's native fontification.
    (doom-themes-org-config)

    (load-theme 'doom-monokai-pro t)
   )

  ;; for doom-monokai-pro, set the line numbers to be just a little lighter
  (custom-set-faces
    `(line-number ((t (:foreground ,(doom-color 'base5)))))
    `(match ((t (:background ,(doom-color 'dark-blue)))))
    `(company-preview-common ((t
       (:foreground ,(doom-color 'yellow)
        :background ,(doom-color 'base5))))))
#+END_SRC

* Keybindings

#+BEGIN_SRC emacs-lisp
  (use-package general
    :after evil
    :config
    (general-create-definer my/leader-keys
      :states '(normal visual insert emacs)
      :keymaps 'override
      :prefix "SPC"
      :global-prefix "C-SPC") ; Access leader in insert/emacs mode with C-SPC
    (my/leader-keys
       ;; Buffer & File management
       "."    'find-file
       ","    'switch-to-buffer
       "b"    '(:ignore t :which-key "buffer")
       "b b"  'consult-buffer                     ; project buffers
       "b r"  'revert-buffer-quick                ; revert current buffer
       "r"    'consult-recent-file                ; recent files only
       "s"    '(:ignore t :which-key "search")
       "s r"  'consult-ripgrep                    ; ripgrep in project
       "s l"  'consult-line-multi                 ; search open buffers
       "i"    'consult-imenu                      ; navigate code structure
       "y"    'consult-yank-pop                   ; browse kill ring
       ";"    'consult-complex-command            ; command history

       ;; Search & Navigation
       "/"    'consult-line                       ; search current buffer
       "?"    'consult-line-multi                 ; search across buffers
       "i"    'consult-imenu                      ; jump to functions/classes
       "I"    'consult-imenu-multi                ; imenu across project
       "j"    '(:ignore t :which-key "jump")
       "j m"  'consult-mark                       ; jump to marks in buffer
       "j M"  'consult-global-mark                ; jump to global marks
       "j o"  'consult-outline                    ; navigate by headings

       ;; Code utilities
       "c"    '(:ignore t :which-key "code")
       "c r"  'eglot-rename
       "c D"  'eglot-find-implementation
       "c t"  'eglot-find-typeDefinition

       ;; Project utilities
       "p"    '(:ignore t :which-key "project")
       "p f"  'project-find-file
       "p b"  'project-switch-to-buffer
       "p r"  'project-query-replace-regexp
       "p s"  'my/project-desktop-save
       "p l"  'my/project-desktop-read
       "p L"  'my/project-desktop-list
    )
    (general-define-key
       :states '(insert normal global)
       ;; override prefixes that I commonly mistype
       "C-x C-o" 'other-window
       "C-x C-k" 'kill-buffer
       "C-x C-b" 'switch-to-buffer
       "C-x C-z" nil ; prevent accidentally pressing this sequence from freezing emacs over CRD

       ;; scroll view without moving point
       "M-<up>" 'scroll-up-line
       "M-<down>" 'scroll-down-line

       "C-/" 'comment-line
    )
  )
#+END_SRC
